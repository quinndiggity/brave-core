declare_args() {
  use_shared_lib = false
  rustc_flags = ""
  rustc_target = ""
}

# See https://forge.rust-lang.org/platform-support.html for possible targets
if (is_win) {
  if (current_cpu == "x86") {
    rustc_flags += " --codegen target-feature=+crt-static"
    rustc_target = "i686-pc-windows-msvc"
  } else if (current_cpu == "x64") {
    rustc_target = "x86_64-pc-windows-msvc"
  }
} else if (is_mac) {
  if (current_cpu == "x64") {
    rustc_target = "x86_64-apple-darwin"
  }
} else if (is_linux) {
  if (current_cpu == "x64") {
    rustc_target = "x86_64-unknown-linux-gnu"
  }
} else if (is_android) {
  if (current_cpu == "arm") {
    rustc_target = "arm-linux-androideabi"
  } else if (current_cpu == "arm64") {
    rustc_target = "aarch64-linux-android"
  } else if (current_cpu == "x86") {
    rustc_target = "i686-linux-android"
  } else if (current_cpu == "x64") {
    rustc_target = "x86_64-linux-android"
  }
} else if (is_ios) {
  if (current_cpu == "arm64") {
    rustc_target = "aarch64-apple-ios"
  } else if (current_cpu == "x64") {
    rustc_target = "x86_64-apple-ios"
  }
}


speedreader_shared_lib_install_name = ""

if (use_shared_lib && !is_component_build) {
  speedreader_shared_lib_install_name = "@loader_path/../../Libraries/libspeedreader_ffi.dylib"
  rustc_flags += " --codegen link-args=-Wl,-install_name,$speedreader_shared_lib_install_name"
}

speedreader_build_path = "$root_out_dir/speedreader/out"

speedreader_lib_path = speedreader_build_path

if (rustc_target != "") {
  speedreader_lib_path += "/" + rustc_target
}

if (is_debug) {
  speedreader_lib_path += "/debug"
} else {
  speedreader_lib_path += "/release"
}

if (is_win) {
  speedreader_lib_path += "/speedreader.lib"
} else if (is_linux || is_android || is_ios) {
  speedreader_lib_path += "/libspeedreader_ffi.a"
} else if (is_mac) {
  if (use_shared_lib) {
    speedreader_lib_path += "/libspeedreader_ffi.dylib"
  } else {
    speedreader_lib_path += "/libspeedreader_ffi.a"
  }
}
