import("//brave/components/common/typescript.gni")
import("//build/config/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("buildflags/buildflags.gni")

declare_args() {
  brave_sync_endpoint = "https://sync.brave.com"
}

config("brave_sync_config") {
  defines = [
    "BRAVE_SYNC_ENDPOINT=\"$brave_sync_endpoint\""
  ]
}

source_set("prefs") {
  sources = [
    "brave_sync_prefs.cc",
    "brave_sync_prefs.h",
  ]

  deps = [
    "//base",
    "//components/prefs",
    "//components/pref_registry",
  ]
}

source_set("crypto") {
  sources = [
    "crypto/crypto.cc",
    "crypto/crypto.h",
  ]

  deps = [
    "//base",
    "//brave/vendor/bat-native-tweetnacl:tweetnacl",
    "//brave/vendor/bip39wally-core-native:bip39wally-core",
    "//crypto",
    "//third_party/boringssl",
  ]

  if (is_android) {
    deps += [ "//third_party/android_sdk:cpu_features" ]
  }
}

source_set("features") {
  sources = [
    "features.cc",
    "features.h",
  ]

  deps = [
    "//base",
  ]
}

source_set("core") {
  sources = [
    "access_token_consumer.cc",
    "access_token_consumer.h",
    "access_token_fetcher.cc",
    "access_token_fetcher.h",
    "access_token_fetcher_impl.cc",
    "access_token_fetcher_impl.h",
  ]

  deps = [
    ":crypto",
    ":features",
    ":prefs",
    "//base",
    # Only for google_service_auth_error.h
    "//google_apis:google_apis",
    "//net:net",
    "//net/traffic_annotation",
    "//services/network/public/cpp:cpp_base",
    "//services/network/public/cpp:cpp",
    "//url:url",
  ]
}

source_set("brave_sync") {
  sources = [
  ]

  deps = [
    ":core",
    "buildflags",
    "//base",
  ]
}
